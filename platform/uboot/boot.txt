setenv knladdr 0x40008000
setenv dtbaddr 0x5fa00000
setenv loadaddr 0x50008000
setenv dtboaddr 0x52008000

setenv bootargs "
 init=/init rootwait ro androidboot.boottime=223.708 androidboot.selinux=permissive
 androidboot.revision=2.0 androidboot.board_id=0x1234567 init_time=1552948124 androidboot.serialno=${serial#}
 androidboot.slot_suffix=_a firmware_class.path=/vendor/etc/firmware video=HDMI-A-1:e ${debug_bootargs}
"

setenv bootcmd_bcb '
 bcb load $mmc_bootdev misc ;
 bcb test command = bootonce-bootloader && bcb set command boot-fastboot && bcb store && setenv androidrecovery true ;
 bcb test command = boot-recovery && setenv androidrecovery true ;
'

setenv bootcmd_prepare_env '
 setenv bootdevice_path \"__SYSFS_MMC0_PATH__\";
 if test \"${mmc_bootdev}\" = \"1\";
 then
  setenv bootdevice_path \"__SYSFS_MMC1_PATH__\";
 fi;
 setenv bootargs \"\$bootargs androidboot.boot_devices=\${bootdevice_path}\" ;
'

setenv bootcmd_start '
 if test \"${androidrecovery}\" != \"true\";
 then
  setenv bootargs \"\$bootargs androidboot.force_normal_boot=1\" ;
 fi;
 abootimg addr \$loadaddr
 abootimg get recovery_dtbo dtbo_addr
 adtimg addr \${dtbo_addr}
 adtimg get dt --index=0 dtb_start dtb_size &&
 cp.b \$dtb_start \$dtbaddr \$dtb_size &&
 fdt addr \$dtbaddr &&
 adtimg get dt --index=1 dtb_start dtb_size &&
 cp.b \$dtb_start \$dtboaddr \$dtb_size &&
 fdt resize 8192 &&
 fdt apply \$dtboaddr &&
 setenv bootargs \"\$bootargs androidboot.dtbo_idx=0,1\" ;
 bootm \$loadaddr
'

setenv bootcmd_block '
 run bootcmd_bcb &&
 part start mmc \$mmc_bootdev boot_a boot_start &&
 part size mmc \$mmc_bootdev boot_a boot_size &&
 mmc dev \$mmc_bootdev &&
 mmc read \$loadaddr \$boot_start \$boot_size
'

setenv bootcmd '
 run bootcmd_prepare_env ;
 run bootcmd_block ;
 run bootcmd_start ;
'

run bootcmd
